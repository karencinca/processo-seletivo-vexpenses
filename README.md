# Desafio VExpenses - DevOps 

Candidata: **Karen Aires da Silva Cinca** 

‚ú®<a href="#mag_right-an√°lise-e-descri√ß√£o-t√©cnica-do-c√≥digo-terraform">An√°lise e Descri√ß√£o T√©cnica do C√≥digo Terraform</a>

‚ú®<a href="#wrench-modifica√ß√£o-e-melhoria-do-c√≥digo-terraform">Modifica√ß√£o e Melhoria do C√≥digo Terraform</a>

‚ú®<a href="/main.tf">Arquivo modificado</a>

‚ú®<a href="#bulb-observa√ß√µes">Observa√ß√µes</a>

‚ú®<a href="#key-instru√ß√µes">Instru√ß√µes</a>

Este reposit√≥rio cont√©m o desafio proposto pela **VExpenses Despesas Corporativas**, que √© uma das etapas do processo seletivo para a vaga de Est√°gio em DevOps. O objetivo √© demonstrar conhecimentos em IaC (Infraestrutura como C√≥digo) utilizando Terraform como meio de automatizar a configura√ß√£o de servidores. Tamb√©m tem o prop√≥sito de expressar conhecimento em gerenciamento de recursos da AWS, priorizando a seguran√ßa e a manuten√ß√£o dos recursos. 


## :mag_right: An√°lise e Descri√ß√£o T√©cnica do C√≥digo Terraform

No primeiro trecho de c√≥digo √© definida a AWS como *cloud provider*, selecionando a regi√£o `us-east-1` (Norte da Virg√≠nia, regi√£o Leste dos EUA), onde ser√° provisionada a infraestrutura:

```tf
provider "aws" {
  region = "us-east-1"
}
```
Depois s√£o criadas vari√°veis que armazenam nome do projeto e nome do candidato para fazer a concatena√ß√£o para a cria√ß√£o dos nomes ao longo do projeto:

```tf
variable "projeto" {
  description = "Nome do projeto"
  type        = string
  default     = "VExpenses"
}


variable "candidato" {
  description = "Nome do candidato"
  type        = string
  default     = "SeuNome"
}
```

√â criada, ent√£o, uma chave privada usando o algoritmo RSA de 2048 bits, assim √© poss√≠vel baix√°-la e us√°-la para se conectar ao servidor/inst√¢ncia EC2 via SSH de forma segura e sem precisar de senha:

```tf
resource "tls_private_key" "ec2_key" {
  algorithm = "RSA"
  rsa_bits  = 2048
}
```

Tamb√©m, √© criada a *key pair* gerando a chave p√∫blica a partir da chave privada previamente declarada para permitir acesso √† EC2, com nome baseado nas vari√°veis `${var.projeto}` e `${var.candidato}`:

```tf
resource "aws_key_pair" "ec2_key_pair" {
  key_name   = "${var.projeto}-${var.candidato}-key"
  public_key = tls_private_key.ec2_key.public_key_openssh
}
```

Em seguida, √© declarado um recurso do tipo VPC com o nome `main_vpc`, criando uma rede isolada dentro da AWS servindo de alicerce para os recursos a serem criados. Depois √© definido o bloco de endere√ßos IP dispon√≠veis para a VPC, o que significa que ela pode ter at√© 65.536 IPs privados (10.0.0.0 at√© 10.0.255.255). Al√©m disso, a resolu√ß√£o de DNS √© ativada, permitindo que a VPC use nomes de dom√≠nio internos em vez de apenas endere√ßos IP. Tamb√©m, permite que as inst√¢ncias dentro da VPC recebam nomes DNS p√∫blicos, essencial para acesso externo. Por √∫ltimo, √© adicionada uma tag unindo o nome do projeto e o nome do candidato para facilitar a identifica√ß√£o no console:

```tf
resource "aws_vpc" "main_vpc" {
  cidr_block           = "10.0.0.0/16"
  enable_dns_support   = true
  enable_dns_hostnames = true

  tags = {
    Name = "${var.projeto}-${var.candidato}-vpc"
  }
}
```

A seguir, um recurso do tipo *subnet* √© definido com o nome `main_subnet`. Como a *subnet* precisa estar dentro de uma VPC, √© usado o ID da VPC j√° criada (`main_vpc`) para estabelecer que a *subnet* estar√° dentro dela. Com o `cidr_block` √© definido o intervalo de endere√ßos IP dentro da *subnet*, o que significa que essa *subnet* pode ter at√© 256 IPs (de 10.0.1.1 at√© 10.0.1.255). Esse bloco CIDR precisa estar dentro do bloco CIDR da VPC, que foi antes indicado como 10.0.0.0/16, sendo assim, um peda√ßo desse espa√ßo foi reservado para essa *subnet*. Em `availability_zone` √© selecionada em qual zona de disponibilidade a *subnet* ser√° criada, que nesse caso ficar√° no Leste dos EUA. Por √∫ltimo, √© adicionada uma tag unindo o nome do projeto e o nome do candidato para facilitar a identifica√ß√£o no console:

```tf
resource "aws_subnet" "main_subnet" {
  vpc_id            = aws_vpc.main_vpc.id
  cidr_block        = "10.0.1.0/24"
  availability_zone = "us-east-1a"

  tags = {
    Name = "${var.projeto}-${var.candidato}-subnet"
  }
}
```

Depois, um recurso do tipo Internet Gateway chamado `main_igw` √© definido. Ele associa o Internet Gateway √† VPC criada anteriormente atrav√©s do ID da VPC. Dessa forma, a VPC √© conectada √† internet (apesar de precisar da *route table* para funcionar). Depois, √© adicionada uma tag unindo o nome do projeto e o nome do candidato para facilitar a identifica√ß√£o no console:

```tf
resource "aws_internet_gateway" "main_igw" {
  vpc_id = aws_vpc.main_vpc.id

  tags = {
    Name = "${var.projeto}-${var.candidato}-igw"
  }
}
```

Em seguida, √© criada uma *route table* chamada `main_route_table` associando √† VPC anteriormente criada. Ao utilizar "0.0.0.0/0" no `cidr_block`, significa que essa rota √© para todo o tr√°fego externo. Depois esse tr√°fego √© enviado para o *Internet Gateway* j√° definido. Por √∫ltimo, √© adicionada uma tag para facilitar a visualiza√ß√£o no console:

```tf
resource "aws_route_table" "main_route_table" {
  vpc_id = aws_vpc.main_vpc.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.main_igw.id
  }

  tags = {
    Name = "${var.projeto}-${var.candidato}-route_table"
  }
}
```
A *route table* √© associada com a *subnet* j√° definida, permitindo os recursos acessarem a internet. Por √∫ltimo, √© adicionada uma tag que seria para facilitar a visualiza√ß√£o no console, mas esse argumento n√£o √© v√°lido para o recurso `aws_route_table_association`:

```tf
resource "aws_route_table_association" "main_association" {
  subnet_id      = aws_subnet.main_subnet.id
  route_table_id = aws_route_table.main_route_table.id

  tags = {
    Name = "${var.projeto}-${var.candidato}-route_table_association"
  }
}
```
Posteriormente, √© definido um grupo de seguran√ßa criado para controlar quais conex√µes podem entrar e sair para os recursos da VPC (*subnets* e *route tables* associadas). No `ingress` s√£o declaradas as regras de entrada, que nesse caso est√° permitindo conex√µes SSH (porta 22) de qualquer IP, visto que o `cidr_blocks` est√° como "0.0.0.0/0".

No `egress` est√£o as configura√ß√µes de sa√≠da, que nesse caso est√° da forma padr√£o, ou seja, √© permitida a conex√£o de qualquer porta e qualquer IP, assim a inst√¢ncia EC2 pode acessar qualquer servi√ßo na internet (como baixar atualiza√ß√µes, acessar APIs, entre outros exemplos). Por √∫ltimo, √© adicionada a tag para facilitar a visualiza√ß√£o no console:

```tf
resource "aws_security_group" "main_sg" {
  name        = "${var.projeto}-${var.candidato}-sg"
  description = "Permitir SSH de qualquer lugar e todo o trafego de saida"
  vpc_id      = aws_vpc.main_vpc.id

  # Regras de entrada
  ingress {
    description      = "Allow SSH from anywhere"
    from_port        = 22
    to_port          = 22
    protocol         = "tcp"
    cidr_blocks      = ["0.0.0.0/0"]
    ipv6_cidr_blocks = ["::/0"]
  }

  # Regras de sa√≠da
  egress {
    description      = "Allow all outbound traffic"
    from_port        = 0
    to_port          = 0
    protocol         = "-1"
    cidr_blocks      = ["0.0.0.0/0"]
    ipv6_cidr_blocks = ["::/0"]
  }

  tags = {
    Name = "${var.projeto}-${var.candidato}-sg"
  }
}
```

Em seguida, √© feita a busca pela vers√£o mais recente do Debian 12. O *owner ID* refor√ßa a busca pela AMI (*Amazon Machine Image*) esperada, sendo assim, √© selecionado o Debian 12 como o sistema operacional da inst√¢ncia:

```tf
data "aws_ami" "debian12" {
  most_recent = true

  filter {
    name   = "name"
    values = ["debian-12-amd64-*"]
  }

  filter {
    name   = "virtualization-type"
    values = ["hvm"]
  }

  owners = ["679593333241"]
}
```

Ap√≥s isso, √© declarado um recurso `aws_instance` a fim de subir uma inst√¢ncia EC2 do tipo `t2.micro` (1 vCPU, 1GB RAM) e que ser√° criada dentro da *subnet* definida anteriormente. O `key_name` permite acessar a EC2 usando a chave j√° criada com `aws_key_pair`. Depois, √© associado o grupo de seguran√ßa j√° criado anteriormente, permitindo acesso via SSH e sa√≠da para a internet. 

Para permitir a conex√£o via SSH de qualquer lugar, a EC2 recebe um IP p√∫blico para acessar a internet.
Por √∫ltimo, √© definido que a EC2 ter√° um disco de 20GB no tipo gp2 (SSD padr√£o) e que o disco ser√° exclu√≠do quando a EC2 for deletada:

```tf
resource "aws_instance" "debian_ec2" {
  ami             = data.aws_ami.debian12.id
  instance_type   = "t2.micro"
  subnet_id       = aws_subnet.main_subnet.id
  key_name        = aws_key_pair.ec2_key_pair.key_name
  security_groups = [aws_security_group.main_sg.name]

  associate_public_ip_address = true

  root_block_device {
    volume_size           = 20
    volume_type           = "gp2"
    delete_on_termination = true
  }
```

Depois, √© declarado o argumento `user_data`, que executa um script automaticamente quando a inst√¢ncia √© iniciada. Nesse caso, ele atualiza a lista de pacotes e atualiza as depend√™ncias do sistema operacional. Para facilitar a identifica√ß√£o da EC2, √© adicionada uma tag usando o nome do projeto e o nome do canditado:

```tf
  user_data = <<-EOF
              #!/bin/bash
              apt-get update -y
              apt-get upgrade -y
              EOF

  tags = {
    Name = "${var.projeto}-${var.candidato}-ec2"
  }
}
```
Ent√£o, √© gerado o *output* da chave privada usada para conectar via SSH. A chave √© ocultada no terminal por seguran√ßa:

```tf
output "private_key" {
  description = "Chave privada para acessar a inst√¢ncia EC2"
  value       = tls_private_key.ec2_key.private_key_pem
  sensitive   = true
}
```

Por fim, √© feito o *output* do IP p√∫blico da inst√¢ncia EC2 que foi criada. Esse IP √© gerado automaticamente pela AWS:

```tf
output "ec2_public_ip" {
  description = "Endere√ßo IP p√∫blico da inst√¢ncia EC2"
  value       = aws_instance.debian_ec2.public_ip
}
```
Apesar do c√≥digo apresentado possuir os comandos essenciais para a cria√ß√£o de uma inst√¢ncia na AWS, alguns trechos n√£o condizem com o que a documenta√ß√£o do Terraform orienta. Tais peculiaridades s√£o exploradas na se√ß√£o de Modifica√ß√£o e Melhoria do C√≥digo Terraform.


## :wrench: Modifica√ß√£o e Melhoria do C√≥digo Terraform

### Chave privada para conex√£o via SSH
Como j√° foi mencionado, a inst√¢ncia EC2 possui uma chave privada para que possa acontecer a conex√£o via SSH. Foi inserido o c√≥digo abaixo para fazer com que essa chave seja salva automaticamente no arquivo do projeto assim que ele √© executado e, assim, facilitar o processo de conex√£o:
```tf
resource "local_file" "TF-key" {
  content  = tls_private_key.ec2_key.private_key_pem
  filename = "tfkey"
}
```

### Route Table Connection sem tags
No c√≥digo apresentado, √© criada uma tag `Name` no recurso referente √† conex√£o das tabelas de rotas, entretando, esse comando n√£o √© v√°lido para essa situa√ß√£o. Sendo assim, esse trecho de c√≥digo foi retirado. 

### Regras de entrada e sa√≠da no grupo de seguran√ßa
As regras de entrada e sa√≠da no grupo de seguran√ßa foram escritas usando o `ingress` e o `egress` como argumentos do recurso `aws_security_group` no c√≥digo original. Por√©m, a fim de evitar conflitos de regras envolvendo blocos CIDR, o c√≥digo foi alterado, passando a utilizar um recurso para cada regra de entrada e sa√≠da:

```tf
resource "aws_security_group" "main_sg" {
  name        = "${var.projeto}-${var.candidato}-sg"
  description = "Permitir SSH e HTTP de qualquer lugar e todo o trafego de saida"
  vpc_id      = aws_vpc.main_vpc.id
}

resource "aws_vpc_security_group_ingress_rule" "allow_ssh_key" {
  security_group_id = aws_security_group.main_sg.id

  cidr_ipv4   = "0.0.0.0/0"
  from_port   = 22
  ip_protocol = "tcp"
  to_port     = 22
}

resource "aws_vpc_security_group_ingress_rule" "allow_http" {
  security_group_id = aws_security_group.main_sg.id

  cidr_ipv4   = "0.0.0.0/0"
  from_port   = 80
  ip_protocol = "tcp"
  to_port     = 80
}

resource "aws_vpc_security_group_egress_rule" "allow_all" {
  security_group_id = aws_security_group.main_sg.id

  cidr_ipv4   = "0.0.0.0/0"
  ip_protocol = "-1"
}
```

Vale observar que no recurso `allow_ssh_key` n√£o foi definido um `cidr_ipv4` para um IP espec√≠fico. O ideal nesse caso seria atribuir para um IP fixo de uma VPN, onde os usu√°rios seriam obrigados a estarem conectados para conseguir acessar o recurso alvo via SSH (uma EC2, por exemplo). Isso serviria como uma camada adicional de seguran√ßa, al√©m do uso de chave assim√©trica para SSH configurada anteriormente nesse desafio. Para fins de conveni√™ncia, foi permitido o acesso SSH de qualquer IP.

### Defini√ß√£o do grupo de seguran√ßa da inst√¢ncia
No c√≥digo apresentado, o grupo de seguran√ßa √© relacionado com a inst√¢ncia a partir do seguinte c√≥digo no `resource "aws-instance"`:

```tf
security_groups = [aws_security_group.main_sg.name]
```
Entretanto, ao criar inst√¢ncias em uma VPC, a rela√ß√£o √© para ser feita atrav√©s do ID do grupo de seguran√ßa, trocando a linha de c√≥digo anterior pela linha abaixo:

```tf
vpc_security_group_ids = [aws_security_group.main_sg.id]
```

### Automa√ß√£o da instala√ß√£o do Nginx
Ao executar uma inst√¢ncia, existe a op√ß√£o de passar dados que podem ser usados para realizar tarefas de configura√ß√£o comuns automatizadas ou executar scripts logo ap√≥s a inicializa√ß√£o da inst√¢ncia. Dessa forma, foram utilizados comandos de instala√ß√£o do Nginx no `user_data`, que fica no recurso da `aws_instance`. Assim, o Ngnix √© instalado e iniciado automaticamente ap√≥s a cria√ß√£o da inst√¢ncia. 
Para fins de organiza√ß√£o do c√≥digo, foi criado um arquivo separado com o script de instala√ß√£o e inicializa√ß√£o do Nginx: 

```tf
user_data = file("./install-nginx.sh")
```

Arquivo `install-nginx.sh`:

```bash
#!/bin/bash
sudo apt-get update -y
sudo apt-get install -y nginx
sudo systemctl start nginx
sudo systemctl enable nginx
```

Para fins de assertividade, o comando `apt-get upgrade -y` foi removido, visto que ele n√£o √© necess√°rio para o desafio proposto. Al√©m disso, h√° o risco do comando causar conflitos ou erros, pois atualiza todas as depend√™ncias do sistema operacional de forma indiscriminada, podendo causar efeitos colaterais indesejados. O recomendado √© sempre atualizar as depend√™ncias verificando as suas vers√µes uma a uma.

Para que seja poss√≠vel acessar o Nginx no navegador, foi adicionada uma regra de entrada no grupo de seguran√ßa, sendo permitido o acesso √† inst√¢ncia via HTTP:

```tf
resource "aws_vpc_security_group_ingress_rule" "allow_http" {
  security_group_id = aws_security_group.main_sg.id

  cidr_ipv4   = "0.0.0.0/0"
  from_port   = 80
  ip_protocol = "tcp"
  to_port     = 80
}
```

Se o Nginx for utilizado como um proxy reverso de um *web server* servindo requisi√ß√µes HTTP, ainda √© poss√≠vel liberar a porta 443 para o protocolo TCP/IP a fim de prover HTTPS para as requisi√ß√µes, uma vez que o TLS for corretamente configurado no Nginx.

## :bulb: Observa√ß√µes
Tendo em vista a utiliza√ß√£o do Terraform com GitHub Actions, uma sugest√£o √© utilizar o [OpenTofu](https://opentofu.org/) (um fork do Terraform), j√° que ele usa licen√ßa MPL, que √© mais permissiva do que a licen√ßa utilizada atualmente pelo Terraform da HashiCorp.

##  :key: Instru√ß√µes
Considerando que o Terraform e a AWS CLI (AWS Command Line Interface) j√° est√£o instalados e configurados, seguir os seguintes passos para executar o projeto:

1. Clone o projeto:
```
$ git clone https://github.com/karencinca/processo-seletivo-vexpenses
```

2. Acesse a pasta do projeto:
```
$ cd processo-seletivo-vexpenses
```
3. Inicialize o Terraform:
```
$ terraform init
```
4. Veja o plano de execu√ß√£o do Terraform:
```
$ terraform plan
```
5. Execute as a√ß√µes do Terraform:
```
$ terraform apply
```

Com esses passos, a inst√¢ncia √© criada na plataforma da AWS.
Para fazer a conex√£o via SSH, dentro do diret√≥rio do reposit√≥rio, execute o seguinte comando no terminal, colocando no `ec2_public_ip` o endere√ßo IP que foi gerado:
```
$ ssh -i tfkey admin@ec2_public_ip
```

Para conectar via HTTP, basta acessar o endere√ßo IP no navegador.

##

###### Feito com üíô por [Karen Cinca](https://www.linkedin.com/in/karencinca/).
